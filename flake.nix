{
  description = "yvnth's NixOS config";

  inputs = {
    home-manager = {
      url = "github:nix-community/home-manager";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    lanzaboote = {
      url = "github:nix-community/lanzaboote/v1.1.0";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    mangowm = {
      url = "github:mangowm/mango";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    nix-flatpak = {
      url = "github:gmodena/nix-flatpak/?ref=latest";
    };

    nixpkgs = {
      url = "github:nixos/nixpkgs/nixos-unstable";
    };

    fast-nix-gc = {
      url = "github:Mic92/fast-nix-gc";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    sops-nix = {
      url = "github:Mic92/sops-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    spicetify-nix = {
      url = "github:Gerg-L/spicetify-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    stylix = {
      url = "github:nix-community/stylix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    yvnthx = {
      url = "github:yvnth/yvnthx";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    {
      home-manager,
      lanzaboote,
      mangowm,
      nix-flatpak,
      nixpkgs,
      fast-nix-gc,
      spicetify-nix,
      stylix,
      yvnthx,
      ...
    }@inputs:
    {
      nixosConfigurations.satella = nixpkgs.lib.nixosSystem {
        system = "x86_64-linux";

        specialArgs = {
          inherit inputs;
        };

        modules = [
          ./hosts/satella/configuration.nix
          lanzaboote.nixosModules.lanzaboote
          mangowm.nixosModules.mango
          nix-flatpak.nixosModules.nix-flatpak
          home-manager.nixosModules.home-manager
          stylix.nixosModules.stylix
          fast-nix-gc.nixosModules.default

          {
            nixpkgs.overlays = [
              (final: prev: {
                xdg-desktop-portal-wlr = prev.xdg-desktop-portal-wlr.overrideAttrs (old: rec {
                  version = "0.8.2";
                  src = prev.fetchFromGitHub {
                    owner = "emersion";
                    repo = "xdg-desktop-portal-wlr";
                    rev = "v${version}";
                    hash = "sha256-HITf/hgiASWvn/z49mzS8IS1vuyXwdk1JiAOOHRSQMo=";
                  };
                });
              })
            ];
          }

          {
            home-manager = {
              backupFileExtension = "bak";

              extraSpecialArgs = {
                inherit inputs;
              };

              useGlobalPkgs = true;
              useUserPackages = true;

              users.yvnth = {
                imports = [
                  ./hosts/satella/home.nix
                  inputs.sops-nix.homeManagerModules.sops
                  spicetify-nix.homeManagerModules.default
                  yvnthx.homeManagerModules.default
                ];
              };
            };
          }
        ];
      };
    };
}
